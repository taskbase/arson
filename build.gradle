plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'com.taskbase.arson'
archivesBaseName = 'arson'
version '1.0'

apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.3.41'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'Arson'
                packaging 'jar'
                description 'Kotlin reflection functionalities. Adds missing default values and checks for null ' +
                        'values that are illegal according to the Kotlin type system. Works without additional ' +
                        'Annotations. '
                url 'https://github.com/taskbase/arson/'

                scm {
                    connection 'scm:git:https://github.com/taskbase/arson'
                    developerConnection 'scm:git:https://github.com/taskbase/arson'
                    url 'scm:git:https://github.com/taskbase/arson'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'jjoller'
                        name 'Jost Joller'
                        email 'jost@taskbase.com'
                    }
                }
            }
        }
    }
}